/*
 * Importa o spring boot gradle plugin
 */
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

group = 'eteg'

/*
 * Aplica outros plugins
 */
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'

/*
 * Configura o artefato gerado pela build.
 */
jar {
    baseName = 'opbp-backend'
    version =  '0.0.1'
}

springBoot {
  mainClass = 'br.com.eteg.opbp.Application'
}

bootRun {
  args = ['--spring.profiles.active=dev']
}

/*
 * Taks
 */
task buildDocker(type: Docker, dependsOn: build) {
  push = true
  applicationName = jar.baseName
  dockerfile = file('src/main/docker/Dockerfile')
  doFirst {
    copy {
      from jar
      into stageDir
    }
  }
}

/*
 * Configura os repositórios
 */
repositories {
    mavenCentral()
}

/*
 * Configura o código fonte
 */
sourceCompatibility = 1.8
targetCompatibility = 1.8
 
dependencies {
    compile project(':opbp-frontend')
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
	compile 'org.springframework.boot:spring-boot-starter-security'
    runtime 'com.h2database:h2'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}